Source file for the wind turbine propeller designer:

A BASIC COMPUTER PROGRAM to help you design windmill rotor blades.

(Extract from ‘Scrapyard Windpower Realities’ by Hugh Piggott)



10 PRINT"THIS PROGRAM WILL DESIGN YOUR PROPELLER": PRINT
20 INPUT"TIP SPEED RATIO "; TSR
25 IF TSR>15 THEN PRINT "That's ambitious!"
30 INPUT"NUMBER OF BLADES "; B
35 IF TSR*B>24 THEN PRINT "Too many blades?"
40 PRINT: PRINT" CHOOSE: "
50 PRINT"1.FIND BEST DIAMETER TO MATCH GENERATOR"
60 PRINT"2.CHOOSE YOUR OWN DIAMETER"
70 INPUT a$
80 A=VAL(A$): ON A GOSUB 100,200
90 END 
100 PRINT"FINDING BEST DIAMETER"
110 INPUT"GENERATOR RATED POWER (watts) ";P
120 INPUT"GENERATOR RATED RPM "; RPM
130 INPUT "GEAR RATIO (enter 1 for direct drive)"; RAT
140 RPM=RPM/RAT
150 D=(P*(47*TSR/RPM)^3)^0.2
160 PRINT"DIAMETER IS ";:PRINT D;: PRINT" meters"
165 IF RPM*D>1800 THEN PRINT" *High tip speed-BEWARE EROSION*"
170 PRINT"RATED WINDSPEED IS ";:PRINT RPM*D/20/TSR; : PRINT" m/s"
175 IF RPM*D/TSR>260 THEN PRINT "Try some gearing."
180 PRINT"TORQUE at 3m/s windspeed=";:PRINT 9*D^3/10/TSR^2; : PRINT " Newton metres"
190 GOSUB 210
195 RETURN 
200 INPUT"DIAMETER (meters) ";D
210 INPUT"NUMBER OF STATIONS ";N
220 PRINT"RADIUS SETTING CHORD  WIDTH  'DROP'  THICKNESS"
225 PRINT"meters degrees meters meters meters   meters"
230 FOR X=1 TO N
240  R=D/2/N*X:VAR=R:GOSUB 500
250  FLO=ATN(D/3/TSR/R):VAR=57*FLO-3:GOSUB 500
260  CH=1.7*D^2/B/R/TSR^2*COS(FLO)^2:VAR=CH:GOSUB 500
265  WD=CH*COS(FLO-3/57):VAR=WD:GOSUB 500
270  DP=CH*SIN(FLO-3/57):VAR=DP:GOSUB 500
280  TH=CH/(5+X*3/N):VAR=TH:GOSUB 500
290  PRINT 
300 NEXT X
305 PRINT"WIDTH is the width of the wood during tapering."
310 PRINT"'DROP' is the measurement for 'angling the blade'."
320 RETURN
500 REM printing subroutine
510 VAR=INT(VAR*1000)/1000 
520 VAR$=STR$(VAR)
530 FOR S=1 TO (10-LEN(VAR$)):PRINT" ";: NEXT S
540 PRINT VAR$;
560 RETURN

Above undoubtedly superseded by more current designs!: http://scoraigwind.co.uk/all-of-the-books-by-hugh-how-to-get-them/
